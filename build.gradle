plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group = 'io.github.tumakha'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
	mavenCentral()
	jcenter()
}

ext {
    set('springCloudVersion', 'Hoxton.SR9')
    awsSdkVersion = '1.11.908'
    awsXrayRecorderVersion = '2.7.1'
    springfoxVersion = '3.0.0'
    testcontainersVersion = '1.15.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Swagger
    implementation "io.springfox:springfox-swagger2:${springfoxVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    implementation "io.springfox:springfox-spring-webmvc:${springfoxVersion}"

    // AWS
    implementation 'org.springframework.cloud:spring-cloud-starter-aws'
    // implementation 'org.springframework.cloud:spring-cloud-starter-aws-jdbc'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws-messaging'

    // AWS X-Ray recorder
    implementation 'com.amazonaws:aws-xray-recorder-sdk-core'
    implementation 'com.amazonaws:aws-xray-recorder-sdk-spring'
    implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk'
//	implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-instrumentor'
    implementation 'com.amazonaws:aws-xray-recorder-sdk-apache-http'
    // implementation 'com.amazonaws:aws-xray-recorder-sdk-sql-postgres'
    implementation 'com.amazonaws:aws-xray-recorder-sdk-sql-mysql'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "org.testcontainers:localstack:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation 'org.awaitility:awaitility:4.0.3'
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}") {
            bomProperty 'aws-java-sdk.version', awsSdkVersion
        }
        mavenBom "com.amazonaws:aws-xray-recorder-sdk-bom:${awsXrayRecorderVersion}"
    }
    dependencies {
        dependency "com.amazonaws:aws-java-sdk:${awsSdkVersion}"
    }
}

test {
    environment "AWS_EC2_METADATA_DISABLED", "true"
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        showExceptions = true
        events('passed', 'skipped', 'failed', 'standardOut', 'standardError')
    }
}

bootRun {
    systemProperty "com.amazonaws.sdk.disableEc2Metadata", "true"
    systemProperty "spring.profiles.active", "local"
}
